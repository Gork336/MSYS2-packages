# Maintainer: Gork <bertsmithzp@gmail.com>

pkgname=7zip
pkgver=24.09
pkgrel=1
pkgdesc="File archiver for extremely high compression"
arch=('any')
url="https://www.7-zip.org"
license=('spdx:LGPL-2.1-or-later' 'spdx:BSD-3-Clause' 'LicenseRef-UnRAR')
msys2_references=('archlinux: 7zip')
depends=('gcc-libs')
makedepends=('gcc' 'make')
provides=('p7zip')
conflicts=('p7zip')
replaces=('p7zip')
source=(https://7-zip.org/a/7z${pkgver//./}-src.tar.xz)
sha256sums=('49c05169f49572c1128453579af1632a952409ced028259381dac30726b6133a')

build() {
    bundles=(Alone2 Format7zF SFXCon)
    ui_console="UI/Console"

    for target in "${bundles[@]}"; do
        (
            unset SystemDrive SYSTEMDRIVE
            make -C "CPP/7zip/Bundles/${target}" -j -f ../../cmpl_gcc.mak
        )
    done

    (
        unset SystemDrive SYSTEMDRIVE
        make -C "CPP/7zip/${ui_console}" -j -f ../../cmpl_gcc.mak
    )
}
package() {
    install -Dm755 CPP/7zip/Bundles/Alone2/b/g/7zz "$pkgdir/usr/bin/7zz"
    install -Dm755 CPP/7zip/Bundles/Format7zF/b/g/7z.so "$pkgdir/usr/bin/7z.so"
    install -Dm755 CPP/7zip/Bundles/SFXCon/b/g/7zCon "$pkgdir/usr/bin/7zCon"
    install -Dm755 CPP/7zip/UI/Console/b/g/7z "$pkgdir/usr/bin/7z"

    install -Dm644 DOC/License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 DOC/unRarLicense.txt "$pkgdir/usr/share/licenses/$pkgname/unRarLicense.txt"
}
